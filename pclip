#!/usr/bin/perl

use warnings;
use strict;
use Getopt::Long;
use IO::Select;
use POSIX;
#use Data::Dumper;

my @default_ips = ("dev", "qdev");
my @ips;
my $user = $ENV{USER};
my $delim = "AaAaAaAaAaKAKABbBbBb";
my $shared = "/Users/ozelikov/Downloads/shared-dev/";
my $remote_shared = "~/vm/shared/mac";
my $local_tmp_file = "/tmp/pclip_tmp.txt";
my $localhost = 'localhost';

GetOptions(
    "ip=s" => \@ips,
    "user=s" => \$user,
    "shared=s" => \$shared,
    "help" => sub { print("Usage: pclip [-ip=<ip>]\n"); exit(1); },
);

my $remote_clip_file = "/tmp/pclip_${user}.txt";

sub main
{
    @ips = @default_ips if not @ips;

    info("Starting - ".localtime()."\n");
    info("Remote machine(s): ".join(", ", @ips)."\n");

    my %hosts = map { $_ => {
            ip=>$_,
            fh=>undef,
            child=>undef,
            get=>\&read_remote_clipboard,
            set=>\&copy_to_remote_clipboard,
            prev_data=>undef,
            buffer=>""
        } } @ips;


    push @ips, $localhost;
    $hosts{$localhost} = {
        ip=>$localhost,
        fh=>undef,
        child=>undef,
        get=>\&read_local_clipboard,
        set=>\&copy_to_local_clipboard,
        prev_data=>undef,
    };

    #print(Dumper(\%hosts));

    my $prev_shared_files;

    while (1)
    {
        sync_clipboards(\@ips, \%hosts);

        my @files = sort glob("$shared/*");
        send_new_files($prev_shared_files, \@files);
        $prev_shared_files = \@files;
    }

    info("Stopping\n");
    return 0;
}

sub sync_clipboards
{
    my ($ips, $hosts) = @_;

    for my $ip (@$ips)
    {
        my $host = $hosts->{$ip};

        my $data = $host->{'get'}->($ip, $host);
        last if not defined $data;

        if (not defined $host->{'prev_data'}) {
            $host->{'prev_data'} = $data;
            next;
        }
        elsif ($data eq $host->{'prev_data'}) {
            next;
        }

        $hosts->{$_}->{'prev_data'} = $data foreach @$ips;

        print "****** NEW DATA ON $ip ******\n";
        print "'$data'\n";
        my @ips_to_set = grep { $_ ne $ip } @$ips;
        print ">>> SENDING TO ".join(" ", @ips_to_set)."\n";

        $hosts->{$_}->{'set'}->($hosts->{$_}, $data) foreach @ips_to_set;
    }
}

sub connect_to_remote_xsel
{
    my ($ip) = @_;
    my $ssh_opt = "-o ConnectTimeout=10 -o ServerAliveCountMax=2 -o ServerAliveInterval=60";

    open(my $fh, "ssh $ssh_opt $ip 'while true; do DISPLAY=:0 xsel -o; echo $delim; sleep 1; done'|");

    info("ssh command failed, $!\n") if not $fh;
    info("connected to remote\n") if $fh;

    return $fh;
}

sub connect_to_remote
{
    my ($ip) = @_;
    my $ssh_opt = "-o ConnectTimeout=10 -o ServerAliveCountMax=2 -o ServerAliveInterval=60";
    $ssh_opt = "";
    my $script = "while true; do cat $remote_clip_file 2>/dev/null ; echo $delim; sleep 1; done";

    info("connecting to $ip\n");

    my $pid_file = "/tmp/worker_pid.txt";
    unlink $pid_file;
    open(my $fh, "echo \$\$ > $pid_file; exec ssh $ssh_opt $ip '$script' |");

    info("ssh command failed, $!\n") if not $fh;
    info("connected to $ip\n") if $fh;

    my $worker_pid;
    while (not ($worker_pid = `cat $pid_file 2>/dev/null`))
    {
    }
    info("ssh process pid: $worker_pid\n");

    return ($fh, $worker_pid);
}

sub send_new_files
{
    my ($old_files, $new_files) = @_;

    return if not defined $old_files;

    my %old = map{ $_ => 1 } @$old_files;
    my @added = grep { not $old{$_} } @$new_files;


    my @ignored = grep { $_ =~ /Unconfirmed/ } @added;
    @added = grep { $_ !~ /Unconfirmed/ } @added;

    if (@ignored) {
        print "****** SKIPPING FILES *******\n";
        print("$_\n") foreach @ignored;
        print "*****************************\n";
    }

    if (@added) {
        print "****** COPYING FILES *******\n";
        system("scp '$_' $ips[0]:$remote_shared") foreach @added;
        print "*****************************\n";
    }
}

sub read_remote_clipboard
{
    my ($ip, $host) = @_;
    #info("read_remote_clipboard: $ip\n");

    ($host->{'fh'}, $host->{'child'}) = connect_to_remote($ip) if not $host->{'child'};
    my $fh = $host->{'fh'};
    if (not $fh) {
        print "Failed to connect to $ip, waiting 10sec\n";
        sleep(10);
        return "";
    }

    my $sel = IO::Select->new($fh);

    while (1)
    {
        $! = 0;
        my @ready = $sel->can_read(10);
        if (not @ready) {
            if (not $!) {
                info("can_read timed out\n");
            }
            else {
                info("can_read failed with err $!\n");
            }
            last;
        }

        my $line = <$fh>;
        if (not $line) {
            info("line is empty\n");
            last;
        }
        $host->{'buffer'} .= $line;
        my ($msg, $rest) = split /$delim\n/m, $host->{'buffer'};
        if (defined $msg and defined $rest) {
            $host->{'buffer'} = $rest;
            return $msg;
        }
    }
    info("No message from peer, disconnecting\n");

    # 'close' does wait4 on child ssh process, but after system sleep,
    # ssh process becomes unresponsive and 'close' gets stuck.
    kill 'KILL', $host->{'child'} if $host->{'child'};
    info("Sent SIGKILL to $host->{'child'}\n");
    sleep(1);

    info("Closing connection\n");
    $host->{'fh'} = undef;
    close($fh);
    info("Closed connection\n");
    sleep(10);
    return "";
}

sub read_local_clipboard
{
    # my $out = `powershell.exe Get-Clipboard`;
    my $out = `pbpaste`;
    $out = "" if not defined $out;
    $out =~ s/\r\n$//m;

    #info("got data from local clipboard\n");
    #print $out, "\n";
    return $out;
}

sub copy_to_local_clipboard
{
    my ($host, $remote_data) = @_;
    open(my $fh, ">$local_tmp_file") or die "couldn't open $local_tmp_file for writing, $!";
    print $fh $remote_data;
    close($fh);
    system("pbcopy < $local_tmp_file");
    info("updated local clipboard\n");
}

sub copy_to_remote_clipboard_xsel
{
    my ($host, $data) = @_;
    open(my $fh, "| ssh $host->{ip} 'cat -|DISPLAY=:0 xsel -i'")
        or die "ssh...xsel command failed $!";
    print $fh "$data";
    info("updated remote clipboard\n");
}

sub copy_to_remote_clipboard
{
    my ($host, $data) = @_;
    open(my $fh, "| ssh $host->{ip} 'cat > $remote_clip_file'")
        or die "ssh...cat command failed $!";
    print $fh "$data";
    info("updated remote clipboard on $host->{ip}\n");
}

sub info
{
    my ($msg) = @_;
    my $ts = strftime("%H:%M:%S", localtime);
    print "$ts: $msg";
}

exit main();
